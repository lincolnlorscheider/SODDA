5	 C ********************************************************************************
10	 C					110.RG.PPCL
15	 C ********************************************************************************
20	 C THIS SOFTWARE IS OWNED AND MAINTAINED BY SIEMENS BUILDING TECHNOLOGIES, INC.
25	 C ALL RIGHTS RESERVED. ANY MODIFICATIONS WITHOUT THE EXPRESS, WRITTEN
30	 C CONSENT FROM SIEMENS BUILDING TECHNOLOGIES, INC. MAY VOID THE WARRANTY.
35	 C
40	 C PROGRAM NAME:  110.RG.PPCL
45	 C PANEL: 110PXCM04
50	 C PANEL LOCATION: XXX
55	 C CONTROLS: BUILDING 10 GLYCOL SYSTEM
60	 C
65	 C VER	DATE		NAME				COMMENTS
70	 C  A	10-13-2014	SERGIY KOVALCHUK	INITIAL PROGRAMMING
75	 C  1	MM-DD-YYYY
80	 C  2	MM-DD-YYYY
85	 C  3	MM-DD-YYYY
90	 C  4	MM-DD-YYYY
95	 C  5	MM-DD-YYYY
100	 C
105	 C
200	 C ********************************************************************************
205	 C	TABLE OF CONTENTS
210	 C ********************************************************************************
215	 C 5 HEADER
220	 C 100 REVISION HISTORY
225	 C 200 TABLE OF CONTENTS
230	 C 300 RESIDENT POINT SUMMARY ASSIGNMENTS
235	 C 1000 ON POWER RETURN
240	 C 2000 TIMERS
242	 C 2500 AUTOMATIC FAILUER RESET
245	 C 3000 CALCULATIONS
250	 C 4000 ALARM/FAILURE CHECKS
255	 C 5000 ROTATION
260	 C 6000 START UP
265	 C 20000 SHUTDOWN
270	 C 32000 END OF PROGRAM
300	 C ********************************************************************************
303	 C	RESIDENT POINT SUMMARY ASSIGNMENTS
306	 C ********************************************************************************
309	 C SECNDS - POWER AVAILABLE TIMER
312	 C SECND1 - UNASSIGNED
315	 C SECND2 - CH1 TEMPERATURE ALARM TIMER
318	 C SECND3 - CH2 TEMPERATURE ALARM TIMER
321	 C SECND4 - UNASSIGNED
324	 C SECND5 - UNASSIGNED
327	 C SECND6 - CH1 FAIL TIMER
330	 C SECND7 - CH1 FAIL TIMER
333	 C
336	 C $LOC1 - ZGPD17 ROTTATION
339	 C $LOC2 - ZCPD29 ROTTATION
342	 C $LOC3 - UNASSIGNED
345	 C $LOC4 - ZCPD19 ROTTATION
348	 C $LOC5 - ZCPD21 ROTTATION
351	 C $LOC6 - ZCPD23 ROTTATION
354	 C $LOC7 - ZCPD27 ROTTATION
357	 C $LOC8 - UNASSIGNED
360	 C $LOC9 - UNASSIGNED
363	 C $LOC10 - UNASSIGNED
366	 C $LOC11 - UNASSIGNED
369	 C $LOC12 - UNASSIGNED
372	 C $LOC13 - UNASSIGNED
375	 C $LOC14 - UNASSIGNED
378	 C $LOC15 - UNASSIGNED
381	 C
384	 C *** RESIDENT POINTS LOOK UP TABLE ***
387	 C ZG12SY - B10 GLYCOL SYSTEM ENABLE  	[*]
390	 C 110.RG.SYS.FAIL.RESET - B10 GLYCOL SYSTEM ALARM RESET
393	 C ZS04F - SCU 04 PWFL [OFF FOR 300SEC ON POWERUP, ONLY USED IN LINE 2000]
396	 C OPGLYC - CHLR OP ALRM [10_10157_GLY CHLR STTS]
399	 C ZG1S10 - GLYCOL CHILLER 101 ENABLE 	[*]
402	 C ZG2S10 - GLYCOL CHILLER 102 ENABLE 	[*]
405	 C ZCIT10 - INSTR CHW ST [10_10157_PROCESS_SEC_TEMP]
408	 C ZCIT1A - INST CHST ALARM
411	 C ZG1A10 - GLYCOL CHILLER 101 ALARM
414	 C ZG2A10 - GLYCOL CHILLER 102 ALARM
417	 C 110.RG.RU3301.RU3302.CH.ALM - GLYCOL CHILLER ALARM
420	 C ZGPF17 - PUMP 1017 FAIL FLAG
423	 C ZGPF18 - PUMP 1018 FAIL FLAG
426	 C 110.RG.P3317.P3318.ALM - GLYCOL PUMP ALARM
429	 C ZCPF22 - P 1022 FAIL ALM [10_10158_PRCCHILLR COOL PMP_2]
432	 C ZCPF23 - **** POINT NOT FOUND IN THE DATABASE **** IF FOUND UNCOMMENT ALARM LINE
435	 C 110.PCHW.P3621.P3622.ALM - PCHW PUMP ALARM
438	 C FRC.CT.CWP.1009.FAIL - P 1009 FAILURE
441	 C FRC.CT.CWP.1010.FAIL - P 1010 FAILURE
444	 C 110.CW.P2309.P2310.ALM - CWP PUMP ALARM
447	 C ZGXT10 - GY1 2 LCHWT [10_10157_GLYCOL_LEAVING_TEMP]
550	 C ZGXT1A - GLYCOL LEAVING T ALM
553	 C ZLGDY - LED LAG DAY [1-7]
556	 C ZLGTM - LED LAG TIME [HOURS]
559	 C
562	 C ZGPS17 - GLYCOL CHWP 1017  	[*]
565	 C ZGPS18 - GLYCOL CHWP 1018  	[*]
568	 C ZGPD17 - PUMP 1017 LEAD
571	 C ZCPD29 - PUMP 1029 LEAD
574	 C 110.PCW.H3301.TV - PCW TEMP VALVE
577	 C 110.PCW.H3301.TV.V - PCW TEMP VALVE
580	 C ZCITTS - ZCIT10 SET
583	 C ZCIVIG - ZCIV10 IG [INTEGRAL GAIN]
586	 C ZC04SY - CH104 SYSTEM
589	 C ZGXT10 - GY1 2 LCHWT
592	 C ZG12XF - ZGXT10 FAIL
595	 C ZG1W10 - GY101 K WATT
980	 C ********************************************************************************
985	 C	 ON POWER RETURN
990	 C ********************************************************************************
1000	ONPWRT(1020)
1010	GOTO 1502
1020	SET(0,SECNDS,SECND1,SECND2,SECND3,SECND4,SECND5,SECND6,SECND7)
1030	SET(0,$LOC1,$LOC2,$LOC3,$LOC4,$LOC5,$LOC6,$LOC7,$LOC8)
1040	SET(0,$LOC9,$LOC10,$LOC11,$LOC12,$LOC13,$LOC14,$LOC15)
1050	 C
1060	 C
1070	 C
1480	 C RESET ANY ALARMS ON POWER RETURN
1490	GOTO 4650
1500	 C SOFT START ON POWER RETURN, CHECK IF POWER AVAILABLE
1502	SAMPLE(2) "110.BLDG.GY.P.A.S" = "110.BLDG.BUS.A.S"
1504	SAMPLE(2) "110.BLDG.GY.P.B.S" = "110.BLDG.BUS.B.S"
1506	IF("110.BLDG.GY.P.A.S" .EQ. OFF .OR. "110.BLDG.GY.P.B.S" .EQ. OFF) THEN ON(@OPER,ZCPS21) ELSE RELEAS(@OPER,ZCPS21)
1510	IF("110.BLDG.GY.P.A.S" .EQ. OFF .AND. "110.BLDG.GY.P.B.S" .EQ. OFF) THEN SET(0,SECNDS)
1512	"110.GY.SECNDS" = SECNDS
1514	 C SET(@EMER,1,ZCPD21)
1520	IF(SECNDS .LT. 300) THEN ON(ZS04F) ELSE OFF(ZS04F)
1525	 C IF(SECNDS .GE. 1 .AND. SECNDS .LT. 6) THEN ON("110.RG.SYS.FAIL.RESET") ELSE RELE
1526	IF(SECNDS .GE. 1 .AND. SECNDS .LT. 6) THEN GOTO 1527 ELSE GOTO 1529
1527	ON("110.RG.SYS.FAIL.RESET")
1528	SET(@EMER,1,ZCPD21)
1529	IF(SECNDS .GT. 6) THEN RELEAS(@EMER,ZCPD21)
1530	IF(SECNDS .LT. 25) THEN GOTO 20000
1540	 C AFTER DELAY TURN ON SYSTEM POINT
1550	ON("ZG12SY")
1980	 C ********************************************************************************
1985	 C	TIMERS
1990	 C ********************************************************************************
1995	 C TWO CHILLER FAIL TIMER
2000	SAMPLE(1) IF("110.RG.RU3301.RU3302.CH.ALM" .EQ. OFF) THEN SET(0,"110.RG.CH.F.TM") ELSE "110.RG.CH.F.TM" = "110.RG.CH.F.TM" + 1
2010	 C TWO PRI. GLYCOL PUMPS FAIL TIMER
2020	SAMPLE(1) IF("110.RG.P3317.P3318.ALM" .EQ. OFF) THEN SET(0,"110.RG.P17P18.F.TM") ELSE "110.RG.P17P18.F.TM" = "110.RG.P17P18.F.TM" + 1
2030	 C TWO PCW PUMPS FAIL TIMER
2040	SAMPLE(1) IF("110.PCHW.P3621.P3622.ALM" .EQ. OFF) THEN SET(0,"110.RG.P21P22.F.TM") ELSE "110.RG.P21P22.F.TM" = "110.RG.P21P22.F.TM" + 1
2050	 C
2080	SAMPLE(1) IF("110.RG.P1029.P1030.ALM" .EQ. OFF) THEN SET(0,"110.RG.P10P30.F.TM") ELSE "110.RG.P10P30.F.TM" = "110.RG.P10P30.F.TM" + 1
2090	 C
2110	 C
2130	 C
2150	 C
2200	 C CH1 ALARM TIMERS
2210	IF(ZG1S10 .EQ. PRFON .AND. "110.RG.RU3301.RGS.T" .GT. ZG12XF) THEN GOTO 2240 ELSE SECND2 = 0
2220	IF(ZG1S10 .EQ. ON .AND. ZG1W10 .GE. 1) THEN SECND6 = 0
2230	 C CH2 ALARM TIMERS
2240	IF(ZG2S10 .EQ. PRFON .AND. "110.RG.RU3302.RGS.T" .GT. ZG12XF) THEN GOTO 2500 ELSE SECND3 = 0
2250	IF(ZG2S10 .EQ. ON .AND. ZG2W10 .GE. 1) THEN SECND7 = 0
2260	 C
2485	 C ********************************************************************************
2497	 C	AUTOMATIC FAILUER RESET IF BOTH PICES OF REDUNDANT EQUIPMENT FAILED
2498	 C ********************************************************************************
2499	 C
2500	IF("110.RG.CH.F.TM" .LT. 120) THEN GOTO 2560
2510	SET(0,"110.RG.CH.F.TM",SECND2,SECND3,SECND6,SECND7)
2520	RELEAS(@EMER,ZG1F10,ZG2F10,ZG1S10,ZG2S10)
2530	OFF("110.RG.RU3301.RU3302.CH.ALM",ZG1F10,ZG2F10)
2535	RELEAS(@OPER,ZG1D10)
2540	IF(ZG1D10 .EQ. ON) THEN OFF(ZG1D10) ELSE ON(ZG1D10)
2550	 C
2560	IF("110.RG.P17P18.F.TM" .LT. 120) THEN GOTO 2720
2570	SET(0,"110.RG.P17P18.F.TM")
2580	RELEAS(@EMER,ZGPF17,ZGPF18,ZGPS17,ZGPS18)
2690	OFF("110.RG.P3317.P3318.ALM",ZGPF17,ZGPF18)
2700	IF(ZGPD17 .EQ. ON) THEN OFF(ZGPD17) ELSE ON(ZGPD17)
2710	 C
2720	IF("110.RG.P21P22.F.TM" .LT. 120) THEN GOTO 2780
2730	SET(0,"110.RG.P21P22.F.TM")
2740	RELEAS(@EMER,ZCPF21,ZCPF22,ZCPS21,ZCPS22)
2750	OFF("110.PCHW.P3621.P3622.ALM",ZCPF21,ZCPF22)
2760	IF(ZCPD21 .EQ. ON) THEN OFF(ZCPD21) ELSE ON(ZCPD21)
2770	 C
2780	IF("110.RG.P10P30.F.TM" .LT. 120) THEN GOTO 3000
2790	SET(0,"110.RG.P10P30.F.TM")
2800	RELEAS(@EMER,ZCPF29,ZCPF30,ZCPS29,ZCPS30)
2810	OFF("110.RG.P1029.P1030.ALM",ZCPF29,ZCPF30)
2820	IF(ZCPD29 .EQ. ON) THEN OFF(ZCPD29) ELSE ON(ZCPD29)
2830	 C
2965	 C
2985	 C ********************************************************************************
2990	 C	CALCULATIONS
2995	 C ********************************************************************************
2997	 C
3000	ZGXFM = SQRT(ZGXF10) * 18.68
3010	ZCIFM = SQRT(ZCIF10) * 5.93
3020	 C GLYCOL CHILLERS RUNTIME CALCULATIONS
3030	SAMPLE(60) GOTO 3050
3040	GOTO 3070
3050	IF(ZG1S10 .EQ. PRFON) THEN "110.RG.RU3301.RUNTIME" = "110.RG.RU3301.RUNTIME" + 1
3060	IF(ZG2S10 .EQ. PRFON) THEN "110.RG.RU3302.RUNTIME" = "110.RG.RU3302.RUNTIME" + 1
3070	IF("110.RG.RU3301.RUNTIME" .GE. 60) THEN "110.RG.RU3301.RUNTIME.HR" = "110.RG.RU3301.RUNTIME.HR" + 1 ELSE GOTO 3090
3080	SET(0,"110.RG.RU3301.RUNTIME")
3090	IF("110.RG.RU3302.RUNTIME" .GE. 60) THEN "110.RG.RU3302.RUNTIME.HR" = "110.RG.RU3302.RUNTIME.HR" + 1 ELSE GOTO 3110
3100	SET(0,"110.RG.RU3302.RUNTIME")
3105	 C IF CHILLER RUN TIME GREATER THEN 336HR THEN SET OFF NOTIFICATION ALARM
3110	IF("110.RG.RU3301.RUNTIME.HR" .GE. 336 .OR. "110.RG.RU3302.RUNTIME.HR" .GE. 336) THEN ON("110.RG.ROTATION.REC") ELSE OFF("110.RG.ROTATION.REC")
3120	 C RESET RUNTIME OF NON RUNNING CHILLER
3130	IF(ZG1D10 .EQ. ON) THEN SET(0,"110.RG.RU3302.RUNTIME.HR","110.RG.RU3302.RUNTIME") ELSE SET(0,"110.RG.RU3301.RUNTIME.HR","110.RG.RU3301.RUNTIME")
3985	 C ********************************************************************************
3990	 C	ALARM/FAILURE CHECKS
3995	 C ********************************************************************************
3997	 C KEEP CHILLER ALARM FLAG IN "NONE" PRIORITY
4000	IF(OPGLYC .EQ. @OPER) THEN RELEAS(@OPER,OPGLYC)
4010	 C IF CHILLER NOT IN HAND THEN CLEAR ALARM FLAG AND RESET ITS TOTALIZED VALUE
4020	IF(ZG1S10 .EQ. @NONE .AND. ZG2S10 .EQ. @NONE) THEN OFF(OPGLYC) ELSE ON(OPGLYC)
4030	IF(OPGLYC .EQ. OFF) THEN INITTO(0.0,OPGLYC)
4040	 C SUPPLY TEMPERATURE ALARM
4050	IF(ZCIT10 .GT. 65.0) THEN ON(ZCIT1A)
4055	IF(ZCIT10 .LT. 60.0) THEN OFF(ZCIT1A)
4060	 C
4070	IF(ZGXT10 .GT. 35.0) THEN ON(ZGXT1A)
4075	IF(ZGXT10 .LT. 29.0) THEN OFF(ZGXT1A)
4080	 C IF BOTH CHILLERS OR PUMPS IN ALARM THEN TURN ON APPROPRIATE FLAG
4090	IF("ZG1F10" .EQ. ALARM .AND. "ZG2F10" .EQ. ALARM) THEN ON("110.RG.RU3301.RU3302.CH.ALM") ELSE OFF("110.RG.RU3301.RU3302.CH.ALM")
4100	IF("ZGPF17" .EQ. ALARM .AND. "ZGPF18" .EQ. ALARM) THEN ON("110.RG.P3317.P3318.ALM") ELSE OFF("110.RG.P3317.P3318.ALM")
4110	IF("ZCPF21" .EQ. ALARM .AND. "ZCPF22" .EQ. ALARM) THEN ON("110.PCHW.P3621.P3622.ALM") ELSE OFF("110.PCHW.P3621.P3622.ALM")
4120	IF("ZCPF29" .EQ. ALARM .AND. "ZCPF30" .EQ. ALARM) THEN ON("110.RG.P1029.P1030.ALM") ELSE OFF("110.RG.P1029.P1030.ALM")
4160	 C
4170	 C
4180	 C *** GLYCOL PUMPS FAILUER
4190	IF(ZGPS17 .EQ. ON .AND. ZGPS17 .EQ. ALARM) THEN EMON(ZGPF17) ELSE OFF(ZGPF17)
4200	IF(ZGPS18 .EQ. ON .AND. ZGPS18 .EQ. ALARM) THEN EMON(ZGPF18) ELSE OFF(ZGPF18)
4210	 C *** CONDENSER WATER PUMPS FAILURE
4220	IF(ZCPS29 .EQ. ON .AND. ZCPS29 .EQ. ALARM) THEN EMON(ZCPF29) ELSE OFF(ZCPF29)
4230	IF(ZCPS30 .EQ. ON .AND. ZCPS30 .EQ. ALARM) THEN EMON(ZCPF30) ELSE OFF(ZCPF30)
4270	 C *** PCW PUMP FAILURE
4280	IF(ZCPS21 .EQ. ON .AND. ZCPS21 .EQ. ALARM) THEN EMON(ZCPF21) ELSE OFF(ZCPF21)
4290	IF(ZCPS22 .EQ. ON .AND. ZCPS22 .EQ. ALARM) THEN EMON(ZCPF22) ELSE OFF(ZCPF22)
4360	 C *** CH1 ALARMS
4370	IF(ZG1S10 .EQ. ON .AND. ZG1W10.LT. 1 .AND. SECND6 .GT. 30) THEN ON("110.GLY.CHLR101.ALARM") ELSE OFF("110.GLY.CHLR101.ALARM")
4380	IF(SECND2 .GT. ZG12FD) THEN GOTO 4420
4390	IF(ZG1S10 .EQ. ON .AND. ZG1S10 .EQ. ALARM) THEN GOTO 4420
4400	OFF(ZG1F10)
4410	GOTO 4440
4420	EMON(ZG1F10)
4430	 C *** CH2 ALARMS
4440	IF(ZG2S10 .EQ. ON .AND. ZG2W10.LT. 1 .AND. SECND7 .GT. 30) THEN ON("110.GLY.CHLR102.ALARM") ELSE OFF("110.GLY.CHLR102.ALARM")
4450	IF(SECND3 .GT. ZG12FD) THEN GOTO 4490
4460	IF(ZG2S10 .EQ. ON .AND. ZG2S10 .EQ. ALARM) THEN GOTO 4490
4470	OFF(ZG2F10)
4480	GOTO 4600
4490	EMON(ZG2F10)
4500	 C
4510	 C
4585	 C ********************************************************************************
4590	 C 	ORERATOR INITIATED ALARM RESET
4595	 C ********************************************************************************
4600	IF("110.RG.SYS.FAIL.RESET" .EQ. OFF) THEN GOTO 5000
4610	OFF(@OPER,"110.RG.SYS.FAIL.RESET")
4620	RELEAS(@OPER,"110.RG.SYS.FAIL.RESET")
4630	OFF("110.RG.SYS.FAIL.RESET")
4640	 C *** RESET ALL ALARM FLAGS IN THIS SECTION (LINES 4650-4980)
4645	 C RELEAS AND OFF FAIL FLAGS IN EMERGENCY OVERIDE
4650	RELEAS(@EMER,ZG1F10,ZG2F10,ZGPF17,ZGPF18,ZCPF29,ZCPF30,ZCPF21,ZCPF22)
4655	RELEAS(@EMER,ZG1S10,ZG2S10,ZGPS17,ZGPS18,ZCPS29,ZCPS30,ZCPS21,ZCPS22)
4660	OFF(ZG1F10,ZG2F10,ZGPF17,ZGPF18,ZCPF29,ZCPF30,ZCPF21,ZCPF22)
4670	SET(0,SECND2,SECND3,SECND6,SECND7)
4680	 C RELEAS AND OFF START/STOPS IN EMERGENCY OVERIDE
4690	RELEAS(@EMER,ZG1S10,ZG2S10,ZGPS17,ZGPS18,ZCPS29,ZCPS30,ZCPS21,ZCPS22)
4700	 C
4985	 C ********************************************************************************
4990	 C	SHEDULED ROTATION
4995	 C ********************************************************************************
4997	 C *** SHEDULED GLYCOL PUMP ROTTATION ***
5000	IF(DAY .EQ. ZLGDY .AND. CRTIME .EQ. ZLGTM + 0.0833) THEN GOTO 5010 ELSE GOTO 5050
5010	IF($LOC1 .EQ. ON) THEN GOTO 5070
5020	ON($LOC1)
5030	IF(ZGPD17 .EQ. ON) THEN OFF(ZGPD17) ELSE ON(ZGPD17)
5040	GOTO 5070
5050	OFF($LOC1)
5060	 C *** SHEDULED CONDENCER PUMP ROTTATION ***
5070	IF(DAY .EQ. ZLGDY .AND. CRTIME .EQ. ZLGTM + 0.1666) THEN GOTO 5080 ELSE GOTO 5120
5080	IF($LOC2 .EQ. ON) THEN GOTO 5210
5090	ON($LOC2)
5100	IF(ZCPD29 .EQ. ON) THEN OFF(ZCPD29) ELSE ON(ZCPD29)
5110	GOTO 5210
5120	OFF($LOC2)
5200	 C *** PCW PUMP ROTATION
5210	IF(DAY .EQ. ZLGDY .AND. CRTIME .EQ. ZLGTM + 0.41666) THEN GOTO 5220 ELSE GOTO 5260
5220	IF($LOC5 .EQ. ON) THEN GOTO 6000
5230	ON($LOC5)
5240	IF(ZCPD21 .EQ. ON) THEN OFF(ZCPD21) ELSE ON(ZCPD21)
5250	GOTO 6000
5260	OFF($LOC5)
5410	 C
5985	 C ********************************************************************************
5990	 C	START UP
5995	 C ********************************************************************************
5998	 C SKIP TO SHUT DOWN IF SYSTEM POINT IS OFF OR AFTER POWER RETURN
6000	IF(ZG12SY .EQ. OFF .OR. SECNDS .LT. 25) THEN GOTO 20000
6010	 C *** HEAT EXCHANGER LOOPS
6012	IF(ZCPS21 .NE. PRFON .AND. ZCPS22 .NE. PRFON .AND. ZCIFM .LT. 1) THEN GOTO 6014 ELSE GOTO 6020
6014	SET(100,"110.PCW.H3301.TV")
6016	GOTO 6040
6020	LOOP(128,ZCIT10,"110.PCW.H3301.TV",ZCITTS,450,ZCIVIG,0,1,15,0,75,0)
6030	LOOP(128,ZCIT10,"110.PCW.H3301.TV.V",ZCITTS,500,ZCIVIG,200,1,40,0,100,0)
6040	"B10.AT-167C:HTG LOOPOUT" = "B10.AT-167B:HTG LOOPOUT"
6050	"B10.AT-169:HTG LOOPOUT" = "B10.AT-167B:HTG LOOPOUT"
6060	 C *** GLYCOL PUMPS OPERATION
6070	IF(SECNDS .LT. 15.0) THEN GOTO 6200
6080	IF(ZGPF17 .EQ. OFF) THEN GOTO 6120
6090	EMOFF(ZGPS17)
6100	EMON(ZGPS18)
6110	GOTO 6220
6120	IF(ZGPF18 .EQ. OFF) THEN GOTO 6160
6130	EMOFF(ZGPS18)
6140	EMON(ZGPS17)
6150	GOTO 6220
6160	IF(ZGPD17 .EQ. ON) THEN ON(ZGPS17) ELSE ON(ZGPS18)
6170	IF(ZGPD17 .EQ. ON .AND. ZGPS17 .EQ. PRFON) THEN OFF(ZGPS18)
6180	IF(ZGPD17 .EQ. OFF .AND. ZGPS18 .EQ. PRFON) THEN OFF(ZGPS17)
6190	GOTO 6220
6200	OFF(ZGPS17,ZGPS18)
6210	 C *** CONDENCER WATER PUMPS OPERATION
6220	IF(SECNDS .LT. 20.0 .OR. ZG12SY .EQ. OFF) THEN GOTO 6360
6230	IF(ZG1S10 .EQ. OFF .AND. ZG2S10 .EQ. OFF) THEN GOTO 6360
6240	IF(ZCPF29 .EQ. OFF) THEN GOTO 6280
6250	EMOFF(ZCPS29)
6260	EMON(ZCPS30)
6270	GOTO 6540
6280	IF(ZCPF30 .EQ. OFF) THEN GOTO 6320
6290	EMOFF(ZCPS30)
6300	EMON(ZCPS29)
6310	GOTO 6540
6320	IF(ZCPD29 .EQ. ON) THEN ON(ZCPS29) ELSE ON(ZCPS30)
6330	IF(ZCPD29 .EQ. ON .AND. ZCPS29 .EQ. PRFON) THEN OFF(ZCPS30)
6340	IF(ZCPD29 .EQ. OFF .AND. ZCPS30 .EQ. PRFON) THEN OFF(ZCPS29)
6350	GOTO 6540
6360	OFF(ZCPS29,ZCPS30)
6530	 C *** PCW PUMP OPERATION
6540	IF(SECNDS .LT. 35.0) THEN GOTO 6680
6550	IF(ZC04SY .EQ. OFF) THEN GOTO 6680
6560	IF(ZCPF21 .EQ. OFF) THEN GOTO 6600
6570	EMOFF(ZCPS21)
6580	EMON(ZCPS22)
6590	GOTO 7100
6600	IF(ZCPF22 .EQ. OFF) THEN GOTO 6640
6610	EMOFF(ZCPS22)
6620	EMON(ZCPS21)
6630	GOTO 7100
6640	IF(ZCPD21 .EQ. ON) THEN ON(ZCPS21) ELSE ON(ZCPS22)
6650	IF(ZCPD21 .EQ. ON .AND. ZCPS21 .EQ. PRFON) THEN OFF(ZCPS22)
6660	IF(ZCPD21 .EQ. OFF .AND. ZCPS22 .EQ. PRFON) THEN OFF(ZCPS21)
6670	GOTO 7100
6680	OFF(ZCPS21,ZCPS22)
6690	 C
7095	 C *** CHILLER OPERATION
7100	IF(ZG12SY .EQ. OFF .OR. SECNDS .LT. 25.0) THEN GOTO 7230
7110	IF(ZG1F10 .EQ. OFF) THEN GOTO 7150
7120	EMOFF(ZG1S10)
7130	EMON(ZG2S10)
7140	GOTO 19980
7150	IF(ZG2F10 .EQ. OFF) THEN GOTO 7190
7160	EMOFF(ZG2S10)
7170	EMON(ZG1S10)
7180	GOTO 19980
7190	IF(ZG1D10 .EQ. ON) THEN ON(ZG1S10) ELSE ON(ZG2S10)
7200	IF(ZG1D10 .EQ. ON .AND. ZG1S10 .EQ. PRFON) THEN OFF(ZG2S10)
7210	IF(ZG1D10 .EQ. OFF .AND. ZG2S10 .EQ. PRFON) THEN OFF(ZG1S10)
7220	GOTO 19980
7230	OFF(ZG1S10,ZG2S10)
7240	 C
7250	 C
19980	GOTO 32000
20000	OFF(ZG1S10,ZG2S10)
20010	IF(ZG1S10 .EQ. PRFON .OR. ZG2S10 .EQ. PRFON) THEN GOTO 32000
20020	OFF(ZGPS17,ZGPS18,ZCPS29,ZCPS30,ZCPS22)
20030	SET(0,"110.PCW.H3301.TV","110.PCW.H3301.TV.V")
32000	GOTO 1000
